plugins {
  id 'java'
  id 'org.sonarqube' version '5.1.0.4882'
}

group 'it.francescofiora'
version = '1.0-SNAPSHOT'

allprojects {
  repositories {
    mavenLocal()
    mavenCentral()
  }
  java {
    sourceCompatibility = JavaVersion.VERSION_21
    targetCompatibility = JavaVersion.VERSION_21
  }
}

subprojects {
  apply plugin: 'java'
  apply plugin: 'checkstyle'
  apply plugin: 'jacoco'

  java {
    sourceCompatibility = JavaVersion.VERSION_21
    targetCompatibility = JavaVersion.VERSION_21
  }

  group = rootProject.group
  version = rootProject.version

  checkstyle {
    toolVersion property('checkstyle.version')
    configFile  rootProject.file("config/checkstyle/checkstyle.xml")
    configProperties = [
         'checkstyle.cache.file': "${buildDir}/checkstyle.cache",
    ]
  }
  tasks.withType(Checkstyle) {
    reports {
      xml.required = false
      html.required = true
    }
  }
  checkstyleMain {
    source ='src/main/java'
  }

  test {
    useJUnitPlatform()
    finalizedBy jacocoTestReport
  }

  jacocoTestReport {
    dependsOn test
    reports {
      xml.required = true
      csv.required = false
      html.required = true
      html.outputLocation = file("${buildDir}/reports/jacoco/test")
      xml.outputLocation = file("${buildDir}/reports/jacoco/test/jacocoTestReport.xml")
    }
  }

  sonar {
    properties {
      property "sonar.sourceEncoding", "UTF-8"
      property 'sonar.core.codeCoveragePlugin', 'jacoco'
      property 'sonar.coverage.jacoco.xmlReportPaths', "${buildDir}/reports/jacoco/test/jacocoTestReport.xml"
    }
  }

  task cleanResources(type: Delete) {
    delete "build/resources"
  }
}

wrapper {
  gradleVersion = "8.5"
}
