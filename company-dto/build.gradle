plugins {
  id 'java-library'
  id 'org.openapi.generator' version "7.15.0"
  id 'info.solidsoft.pitest' version '1.15.0'
  id 'org.owasp.dependencycheck' version '8.4.3'
  id 'maven-publish'
}

def springdoc_openapi_version = parent.property('springdoc-openapi.version')
def lombok_version = parent.property('lombok.version')
def openpojo_version = parent.property('openpojo.version')
def jakarta_validation_version = parent.property('jakarta-validation.version')
def unit_test_version = parent.property('unit-test.version')
def assertj_version = parent.property('assertj.version')
def springboot_version = parent.property('springboot.version')

dependencies {
  implementation "org.springdoc:springdoc-openapi-starter-webmvc-ui:${springdoc_openapi_version}"
  implementation "jakarta.validation:jakarta.validation-api:${jakarta_validation_version}"
  implementation 'jakarta.annotation:jakarta.annotation-api:3.0.0'
  implementation "org.springframework.data:spring-data-commons:${springboot_version}"

  implementation "org.projectlombok:lombok:${lombok_version}"
  annotationProcessor "org.projectlombok:lombok:${lombok_version}"

  implementation project(':product-common')

  testImplementation "org.junit.jupiter:junit-jupiter-api:${unit_test_version}"
  testImplementation "org.junit.jupiter:junit-jupiter-engine:${unit_test_version}"
  testImplementation "org.assertj:assertj-core:${assertj_version}"
  testImplementation "com.openpojo:openpojo:${openpojo_version}"
  testImplementation "org.projectlombok:lombok:${lombok_version}"
  testAnnotationProcessor "org.projectlombok:lombok:${lombok_version}"
}

sourceSets {
  main {
    java {
      srcDir(tasks.openApiGenerate)
    }
  }
}

java {
    compileJava.dependsOn tasks.named("openApiGenerate")
    withJavadocJar()
    withSourcesJar()
}

openApiValidate {
  inputSpec = "$projectDir/specs/company-api.yaml"
  recommend = true
}

openApiGenerate {
  generatorName.set("spring")
  inputSpec.set("$projectDir/specs/company-api.yaml")
  apiPackage.set("it.francescofiora.product.company.api.web.api")
  modelPackage.set("it.francescofiora.product.company.dto")
  configOptions.set([
          library            : "spring-boot",
          useOneOfInterfaces : "false",
          interfaceOnly      : "true",
          useTags            : "true",
          useSpringBoot3     : "true",
          skipDefaultInterface : "true",
          hideGenerationTimestamp: "true"
  ])
  globalProperties.set([
          apis   : "",
          models : ""
  ])
}

pitest {
  junit5PluginVersion = '1.2.1'
  pitestVersion = '1.15.2'
}

publishing {
  publications {
    mavenJava(MavenPublication) {
      from components.java
    }
  }
}
